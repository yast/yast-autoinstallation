/**
 * File:	modules/AutoinstStorage.ycp
 * Module:	Auto-Installation
 * Summary:	Storage
 * Authors:	Anas Nashif <nashif@suse.de>
 *
 * $Id$
 */
{
    module "AutoinstStorage";
    textdomain "autoinst";

    import "Storage";
    import "RootPart";
    import "Partitions";
    import "FileSystems";
    import "Summary";
    import "Partitions";  
    import "Popup";
    import "Report";

    // All shared data are in yast2.rpm to break cyclic dependencies
    import "AutoinstData";

    include "partitioning/partition_defines.ycp";

    // Read existing fstab and format partitions, but dont create anything
    // Use same mountpoints etc.
    global boolean read_fstab = false;

    // Fstab options
    global map fstab = $[];

    // Partition plan as parsed from control file
    global list<map> AutoPartPlan = [];

    // Prepared target map from parsed data
    global map<string, map> AutoTargetMap = $[];

    /* default value of settings modified */
    global boolean modified = false;

    global map<string,list> raid2device = $[];

    // some architectures need a boot partition. Do we have one?
    global boolean planHasBoot = false;

    // list of devices to ignore when guessing devices
    global list<string> tabooDevices = [];

    /**
     * Function sets internal variable, which indicates, that any
     * settings were modified, to "true"
     */
    global define void SetModified ()
    {
        y2milestone("SetModified");
        modified = true;
    }

    /**
     * Functions which returns if the settings were modified
     * @return boolean  settings were modified
     */
    global define boolean GetModified ()
    {
        return modified;
    }

    /**
     * Wrapper function for the LibStorage call that can't be used directly
     * in YCP
     * @return integer
     *
     */
    global define integer humanStringToByte( string s, boolean b ) {
        if( size(s) == 0 )
            s = "0b";
        if( findfirstof( s, "bB" ) == nil )
            s = s + "b";
        return Storage::ClassicStringToByte(s);
    }

    include "autoinstall/autopart.ycp";
    include "autoinstall/autoinst_dialogs.ycp";

    global define string find_next_disk(map <string, map> tm,string after,symbol ctype) {
        y2milestone("find_next_disk after:\"%1\" ctype:%2",after,ctype);
        string used_disk = "";
	list meta_types = [ `CT_DMRAID, `CT_MDPART, `CT_DMMULTIPATH ];
	list all_types = meta_types;
	all_types = add( all_types, `CT_DISK );
        if( after == "" && !contains(all_types,ctype) ) {
            foreach (string device, map disk, tm, ``{
                if( contains( meta_types, disk["type"]:`jo ) &&
		    !contains( tabooDevices, device ))
                {
                    used_disk = device;
                }
            });
        }

        if( after == "" && used_disk == "" ) {
            foreach (string device, map disk, tm, ``{
                if (disk["bios_id"]:"" == "0x80" && !contains( tabooDevices, device ) )
                {
                    used_disk = device;
                }
            });
        }

        /* device guessing code enhanced */
        if( used_disk == "" ) {
	    ctype = contains(all_types,ctype) ? ctype : `CT_DISK;
	    list<string> devs = 
		maplist( string dev, map du,
	                 filter( string s, map d, tm, 
				 ``(d["type"]:`CT_NONE==ctype)),
			 ``(dev));
	    y2milestone("find_next_disk devs: %1", devs);
	    boolean found = false;
            foreach (string device, devs, ``{
		if( !found && after != "" && device != after )
		    continue;
		if( device == after )
		    found = true;
		if( device == after || contains( tabooDevices, device ) )
		    continue;
		used_disk = device;
		break;
            });
        }

        y2milestone("find_next_disk device detection: %1", used_disk);
        return used_disk;
    }

    /**
     * Pre-process partition plan and prepare for creating partitions.
     * @return void
     */
    define map<string, map>  set_devices(list<map> storage_config ) {
    y2milestone("entering set_devices with %1",storage_config);	
        boolean first_set = false;
        boolean failed = false;
        map<string, map> auto_targetmap =  listmap (map drive, storage_config, ``{
            string device = "";
            y2milestone("Working on drive: %1", drive);

            // FIXME: Check if physical drives > 1
            if (drive["device"]:"" == "ask"  )
            {
                string dev = DiskSelectionDialog();
                if (dev != nil)
                {
                    first_set = true;
                    device = dev;
                }	

                return  $[device: drive ];
            }

            if (!first_set 
                && ( drive["device"]:"" == ""  
                     || drive["device"]:"" == "ask")
                )
            {
                device = Storage::GetPartDisk();
                y2milestone("device: %1", device );
                first_set = true;
                return $[device: drive ];
            }
            else if (drive["device"]:"" != "")
            {	
                string dev = drive["device"]:"";
                if (dev == "") {
                    dev = "error";
                    y2error("Missing device name in partitioning plan");
                    failed = true;
                }

                return $[dev: drive ];
            }
            
        });

        if (failed)
            return nil;
        
        auto_targetmap =  mapmap (string device, map d, auto_targetmap, ``{
            // Convert from Old Style
            if (haskey(d, "use"))
            {
               y2milestone("converting from \"use\" to new style: %1", device);
               if (d["use"]:"" == "free") {
                   d["prefer_remove"] = false;
               } else if (d["use"]:"" == "all" ) {
                   d["prefer_remove"] = true;
               } else if (d["use"]:"" == "linux" ) {
                   d["keep_partition_num"] = GetNoneLinuxPartitions(device);
                   d["prefer_remove"] = true;
               } else {
                    list<string> uselist = filter(string s,
                                splitstring(d["use"]:"", ","), ``(s!=""));
                    y2milestone("uselist: %1", uselist);
                    list<integer> keeplist = [];
                    list<integer> all = GetAllPartitions(device);
                    y2milestone("all list: %1", all);
                    foreach(integer i , all, ``{
                            if (!contains(uselist, sformat("%1", i)))
                                keeplist=add(keeplist,i);
                        });
                    y2milestone("keeplist: %1", keeplist);
                    d["keep_partition_num"] = keeplist;
                    
                    if (size(keeplist) > 0 )
                        d["prefer_remove"] = true;
               }
            } else {
                d["use"] = "all";
            }
            // see if <usepart> is used and add the partitions to <keep_partition_num>
            foreach(map p, d["partitions"]:[], ``{
                    if (p["usepart"]:-1 != -1 ){
                        d["keep_partition_num"] = add(d["keep_partition_num"]:[], p["usepart"]:-1);
                    }
                    });
            d["keep_partition_num"] = toset(d["keep_partition_num"]:[]);

            return($[device:d]);
            });

        y2milestone("processed autoyast partition plan: %1", auto_targetmap );
        return auto_targetmap;
    }

    global define list GetRaidDevices (string dev, map<string,map> tm) {
        list<map> ret = [];
        foreach( string k, map d, tm, ``{
            if( d["type"]:`CT_UNKNOWN == `CT_DISK ||
                d["type"]:`CT_UNKNOWN == `CT_DMMULTIPATH ) {
                list tmp = filter( map p, d["partitions"]:[],
                                    ``( p["raid_name"]:"" == dev ) );
                ret = (list<map>)union( ret, tmp );
            }
        });
        list dlist = maplist( map p, ret, ``{ return p["device"]:""; } );
        y2milestone("GetRaidDevices dlist = %1 and ret = %2", dlist, ret);
        return dlist;
    }

    global define void SearchRaids (map<string,map> tm) {
        map<string,boolean> already_mapped = $[];

        /*
           optional device order for the raid:
           <device>/dev/md</device>
           <partitions config:type="list">
             <partition>
               <raid_options>
                 <device_order config:type="list">
                    <device>/dev/sdc1</device>
                    <device>/dev/sdb2</device>
                 </device_order>
               </raid_options>
               ...
        */
        map d = AutoTargetMap["/dev/md"]:$[];
        integer counter = 0;
        foreach( map p, d["partitions"]:[], ``{
            if( haskey(p, "raid_options") &&
                p["raid_options","device_order"]:[] != [] ) {
                counter = p["partition_nr"]:counter;
                list<string> order = p["raid_options","device_order"]:[];
                y2milestone("found device_order %1 in raidoptions for /dev/md%2", order, counter);
                raid2device[ sformat("/dev/md%1",counter) ] = order;
                already_mapped[ sformat("/dev/md%1",counter) ] = true;
            }
            counter = counter + 1;
        });
        //raid2device = $[];
        foreach( string k, map d, tm, ``{
            if( d["type"]:`CT_UNKNOWN == `CT_DISK ||
                d["type"]:`CT_UNKNOWN == `CT_DMMULTIPATH ) {
                list<map> tmp = filter( map p, d["partitions"]:[],
                                    ``( p["raid_name"]:"" != "" ) );
                list<string> devMd = maplist( map p, tmp, ``{ return p["raid_name"]:""; } );
                foreach( string dev, devMd, ``{
                    if( already_mapped[dev]:false == true ) {
                        continue;
                    }
                    if( ! haskey( raid2device, dev ) ) {
                        raid2device[dev] = GetRaidDevices(dev,tm);
                    } else {
                        foreach( string k, (list<string>)GetRaidDevices(dev,tm), ``{
                             raid2device[dev] = add(raid2device[dev]:[],k);
                        });
                    }
                });
            }
        });
        y2milestone("SearchRaids raid2device = %1", raid2device);
    }

    /**
     * if mountby is used, we will search for the matching
     * partition here.
     * @return list
     */
    global define list<map> mountBy(list<map> settings) {
        map <string, map> tm = Storage::GetTargetMap();
        y2milestone("Storage::GetTargetMap returns %1",tm);
        settings = maplist( map d, settings, ``{
            string device = d["device"]:"";
            d["partitions"] = maplist( map p, d["partitions"]:[], ``{
                string mountby = "";
                boolean mountByIsList = false;
                if( p["mount"]:"" == "swap" || p["filesystem"]:`none == `swap ) {
                     p["mount"] = "swap";
                     p["filesystem"] = `swap;
                }
                if( haskey(p, "mountby") ) {
                    if( p["mountby"]:`none == `label ) {
                        mountby = "label";
                    } else if( p["mountby"]:`none == `uuid ) {
                        mountby = "uuid";
                    } else if( p["mountby"]:`none == `path ) {
                        mountby = "udev_path";
                    } else if( p["mountby"]:`none == `id ) {
                        mountByIsList = true;
                        mountby = "udev_id";
                    } else {
                        y2milestone("unknown mountby parameter '%1' will be ignored",p["mountby"]:`none);
                    }
                }
                /* reuse partition by "mountby" */
                if( mountby != "" && p["create"]:true == false && !haskey(p, "partition_nr") ) {
                    string label = p[mountby]:"";
                    y2milestone("mountby found for %1=%2 in part=%3",mountby,label,p);
                    map target = tm[device]:$[];
                    if( device == "" ) {
                        y2milestone("searching for the device by %1",mountby);
                        foreach( string deviceName, map tmp_target, tm, ``{
                            foreach( map targetPart, tmp_target["partitions"]:[], ``{
                                if( mountByIsList ? (contains(targetPart[mountby]:[], label)):(targetPart[mountby]:"" == label) ) {
                                    target = tmp_target;
                                    device = deviceName;
                                    y2milestone("device=%1 found",device);
                                    break;
                                }
                            });
                        });
                    }
                    foreach( map targetPart, target["partitions"]:[], ``{
                        if( mountByIsList ? (contains(targetPart[mountby]:[], label)):(targetPart[mountby]:"" == label) ) {
                            y2milestone("%1 found in targetmap",mountby);
                            d["device"] = device; //FIXME: for some reason this does not work
                            p["partition_nr"] = targetPart["nr"]:0;
                            p["usepart"] = targetPart["nr"]:0;
                        }
                    });
                }
                return p;
            });
            return d;
        });
        y2milestone("after mountBy settings=%1",settings);
        return settings;
    }

    /* makes something like: 
            <device>/dev/disk/by-id/edd-int13_dev80</device>
       possible (Bug #82867)
    */
    global define list<map> udev2dev(list<map> settings) {
        map <string, map> tm = Storage::GetTargetMap();
        y2milestone("Target map: %1", tm );
        string last_dev = "";
        settings = maplist( map d, settings, ``{
            string device = d["device"]:"";
            string udev_string = "";
            if( device == "" ) {
	        symbol dtyp = d["type"]:`CT_UNKNOWN;
                d["device"] = find_next_disk(tm,last_dev,dtyp);
                y2milestone("empty device in profile set to %1", d["device"]:"");
            }
            // translation of by-id, by-path, ... device names.
            // was handled in autoyast until openSUSE 11.3
            map<string,any> deviceTranslation = $[];
            if( Storage::GetContVolInfo( d["device"]:"", deviceTranslation ) )
		{
		y2milestone("translated %1 -> %2", d["device"]:"", 
		            deviceTranslation["cdevice"]:"");
                d["device"] = deviceTranslation["cdevice"]:"";
		}

            last_dev = d["device"]:"";
            return d;
        });
        return settings;
    }

    global define list<map> checkSizes(list<map> settings) {
        y2milestone("entering checkSizes with %1",settings);
        map <string, map> tm = Storage::GetTargetMap();
        y2milestone("targetmap = %1",tm);

        settings = maplist( map d, settings, ``{
            if( d["type"]:`x == `CT_DISK ) {
                integer sizeByCyl = 0;
                integer usedSize = 0;
                integer max = 0;
                integer cyl_size = 0;
                foreach( string device, map v, tm, ``{
                    if( device == d["device"]:"" ) {
                        sizeByCyl = v["cyl_count"]:0 * v["cyl_size"]:0;
                        cyl_size = v["cyl_size"]:0;
                        y2milestone("device found in tm. sizeByCyl=%1",sizeByCyl);
                    }
                });
                if( sizeByCyl == 0 ) {
                    y2milestone("not found");
                    return d;
                }
                foreach(map pe, d["partitions"]:[], ``{
                    usedSize = usedSize + pe["size"]:0;
                    if( pe["size"]:0 > max )
                        max = pe["size"]:0;
                });
                if( usedSize > sizeByCyl ) {
                    y2milestone("usedSize too big: %1",usedSize);
                    d["partitions"] = maplist( map pe, d["partitions"]:[], ``{
                        integer s = pe["size"]:0;
                        if( s == max ) {
                            y2milestone("shrinking %1",pe);
                            s = s - ((usedSize - sizeByCyl)/cyl_size+1+size(d["partitions"]:[]))*cyl_size; // 1 cyl buffer per partition
                            if( s < 1 ) {
                                Report::Error( sformat( _("The partition plan configured in your XML profile does not fit on the hard disk. %1MB missing"), (usedSize-sizeByCyl)/(1024*1024) ));
                                break;
                            } else {
                                usedSize = usedSize - s;
                                y2milestone("shrinking to %1",s);
                                pe["size"] = s;
                            }
                        }
                        return pe;
                    });
                }
            }
            return d;
        });
        y2milestone("after checkSizes %1",settings);
        return settings;
    }

    /*
       the resize option in the storage lib requires the new size in the
       "region" format. That format is hardly configureable by humans, so I
       do the translation here
    */
    global define list<map> region4resize( list<map> settings ) {
        // the storage lib requires the region to be set
        // we transform the size to the region here
        map <string, map> tm = Storage::GetTargetMap();
        settings = maplist( map d, settings, ``{
            if( d["type"]:`x == `CT_DISK ) {
                map realDisk = tm[ d["device"]:"" ]:$[];
                d["partitions"] = maplist(map pe, d["partitions"]:[], ``{
                    if( pe["resize"]:false ) {
                        integer currentCyl = realDisk["partitions",pe["partition_nr"]:1-1,"region",1]:0;
                        pe["region"] = realDisk["partitions",pe["partition_nr"]:1-1,"region"]:[];
                        if( issubstring( pe["size"]:"", "%") ) {
                            string percentage = deletechars( pe["size"]:"", "%" );
                            integer newCyl = currentCyl * tointeger(percentage) / 100;
                            pe["region",1] = newCyl;
                        } else {
                            integer new_size = humanStringToByte( pe["size"]:"0", true );
                            integer newCyl = new_size/realDisk["cyl_size"]:1;
                            pe["region",1] = newCyl;
                        }
                        y2milestone("resize partition nr %1 of %2 to region: %3", pe["partition_nr"]:1, d["device"]:"", pe["region"]:[]);
                    }
                    return pe;
                });
            }
            return d;
        });
        y2milestone("after region4resize = %1",settings);
        return settings;
    }

    /*
        the percentage must be calculated to an actual size.
        This is done here
    */
    global define list<map> percent2size( list<map> settings ) {
        map <string, map> tm = Storage::GetTargetMap();
        settings = maplist( map d, settings, ``{
            if( d["type"]:`x == `CT_DISK ) {
                map v = tm[ d["device"]:"" ]:$[];
                d["partitions"] = maplist(map pe, d["partitions"]:[], ``{
                    if( issubstring( pe["size"]:"", "%") ) {
                        string percentage = deletechars( pe["size"]:"", "%" );
                        integer device_size = v["cyl_count"]:0 * v["cyl_size"]:0;
                        pe["size"] = sformat("%1", device_size * tointeger(percentage) / 100);
                        y2milestone("percentage %1 of %2 = %3",percentage,d["device"]:"",pe["size"]:"");
                    }
                    return pe;
                });
            } else if( d["type"]:`x == `CT_LVM ) {
                d["partitions"] = maplist(map pe, d["partitions"]:[], ``{
                    if( issubstring( pe["size"]:"", "%") ) {
                        pe["size"] = deletechars( pe["size"]:"", "%" ); // a size smalle than 101 will be treated as % later
                    }
                    return pe;
                });
            }
            return d;
        });
        y2milestone("after percent2size = %1",settings);
        return settings;
    }

    /*
        if partition type is primary but not set in the profile, set it now
    */
    global define list<map> setPartitionType(list<map> settings) {
        map <string, map> tm = Storage::GetTargetMap();
        settings = maplist( map d, settings, ``{
            if( d["type"]:`x == `CT_DISK ) {
                integer mp = tm[ d["device"]:"xxx","max_primary" ]:0;
                if( mp > 0 ) {
                    d["partitions"] = maplist(map pe, d["partitions"]:[], ``{
                        if( haskey(pe, "partition_nr") && !haskey(pe,"partition_type") && pe["partition_nr"]:-1 <= mp )
                            pe["partition_type"] = "primary";
                        return pe;
                    });
                }
            }
            return d;
        });
        y2milestone("after setPartitionType = %1",settings);
        return settings;
    }


    /**
     * Get all the configuration from a map.
     * When called by inst_auto<module name> (preparing autoinstallation data)
     * the list may be empty.
     * @param settings a list	[...]
     * @return	boolean success
     */
    global define boolean Import(list<map> settings)
    {
        y2milestone("entering Import with %1", settings);
        if (Mode::autoinst()) {
            settings = maplist( map d, settings, ``{
                d["partitions"] = sort( map x, map y, d["partitions"]:[], ``( x["partition_nr"]:99 < y["partition_nr"]:99 ));
                return d;
            });

            // fill tabooDevice list with devices to ignore
            map <string, map> initial_target_map = Storage::GetTargetMap();
            foreach ( map drive, settings, ``{
                if( drive["device"]:"" != "" ) {
                    // if <device> is set, it can not end in the taboo list
                    continue;
                }

                /* XML example
                <drive>
                  <skip_list config:type="list">
                    <listentry>
                      <skip_key>driver</skip_key>
                      <skip_value>usb-storage</skip_value>
                    </listentry>
                    <listentry>
                      <skip_key>size_k</skip_key>
                      <skip_value>1048576</skip_value>
                      <skip_if_less_than config:type="boolean">true</skip_if_less_than>
                    </listentry>
                  </skip_list>
                  ...
                */
                foreach (string device, map disk, initial_target_map, ``{
                    foreach( map<string,any> toSkip, drive["skip_list"]:[], ``{
                        string skipKey   = toSkip["skip_key"]:"__missing_key__";
                        string skipType  = "";

                        if( is(disk[skipKey]:nil, string) )
                            skipType  = "string";
                        else if( is(disk[skipKey]:nil, integer) )
                            skipType = "integer";
                        else if( is(disk[skipKey]:nil, symbol) )
                            skipType = "symbol";
                        else
                            y2error("skipKey '%1' is of unknown type. Will be ignored.", skipKey);

                        if( skipType == "string" ) {
                            string skipValue = toSkip["skip_value"]:"__missing__";
                            if( disk[skipKey]:"__not_found__" == skipValue ) {
                                tabooDevices = add( tabooDevices, device );
                                y2milestone("%1 added to device taboo list (%2 == %3)", device, skipKey, skipValue);
                                break;
                            }
                        } else if( skipType == "integer" ) {
                            integer skipValue       = tointeger(toSkip["skip_value"]:"0");
                            boolean skipValueLess   = toSkip["skip_if_less_than"]:false;
                            boolean skipValueMore   = toSkip["skip_if_more_than"]:false;
                            boolean skipValueEqual  = toSkip["skip_if_equal"]:true;
                            if( skipValueLess && disk[skipKey]:0 < skipValue ) {
                                tabooDevices = add( tabooDevices, device );
                                y2milestone("%1 added to device taboo list (%2 < %3)", device, disk[skipKey]:0, skipValue );
                            } else if( skipValueMore && disk[skipKey]:0 > skipValue ) {
                                tabooDevices = add( tabooDevices, device );
                                y2milestone("%1 added to device taboo list (%2 > %3)", device, disk[skipKey]:0, skipValue );
                            }
                            if( skipValueEqual && disk[skipKey]:0 == skipValue ) {
                                tabooDevices = add( tabooDevices, device );
                                y2milestone("%1 added to device taboo list (%2 == %3)", device, skipKey, skipValue);
                                break;
                            }
                        } else if( skipType == "symbol" ) {
                            string skipValue = toSkip["skip_value"]:"`nothing";
                            if( sformat("%1",disk[skipKey]:`nothing) == skipValue ) {
                                tabooDevices = add( tabooDevices, device );
                                y2milestone("%1 added to device taboo list (%2 == %3)", device, skipKey, skipValue);
                                break;
                            }
                        }
                    });
                });
            });

            settings = udev2dev(settings);
            settings = mountBy(settings);
            settings = region4resize(settings);
            settings = percent2size(settings);
            settings = setPartitionType(settings);
            AutoPartPlan = preprocess_partition_config(settings);
            AutoPartPlan = checkSizes(AutoPartPlan);
        } else {
            settings = maplist( map d, settings, ``{
                if( ! haskey(d,"device") ) {
                    // this is just to satisfy the UI
                    d = add( d, "device", "auto" );
                    y2debug("device 'auto' added");
                }
                return d;
            });
            AutoPartPlan = settings;
        }
        y2milestone("AutoPartPlan: %1", AutoPartPlan );

        return true;
    }

    /**
     * Import Fstab data
     * @param settings Settings Map
     * @return	boolean true on success
     */
    global define boolean ImportAdvanced(map settings)
    {
        y2milestone("entering ImportAdvanced with %1", settings);
        fstab = settings["fstab"]:$[];
        read_fstab = fstab["use_existing_fstab"]:false;

        //AutoinstLVM::ZeroNewPartitions = settings["zero_new_partitions"]:true;
        return true;
    }

    /**
     * return Summary of configuration
     * @return  string configuration summary dialog
     */
    global define string Summary()
    {
        string summary = "";
        summary = Summary::AddHeader(summary, _("Drives"));
        integer num = size(AutoPartPlan);
        summary = Summary::AddLine( summary, sformat(_("Total of %1 drive", "Total of %1 drives", num), num));
        summary = Summary::OpenList(summary);
        foreach(map drive, AutoPartPlan, ``{
            summary = Summary::AddListItem(summary, drive["device"]:_("No specific device configured") );
        });
        summary = Summary::CloseList(summary);
        return summary;
    }

    /**
     * Handle /etc/fstab usage
     * @return boolean
     */
    define boolean handle_fstab()
    {
        y2milestone("entering handle_fstab");

        if (!RootPart::didSearchForRootPartitions)
        {
            UI::OpenDialog (`opt(`decorated ),
                            `Label(_("Evaluating root partition. One moment please...")));
            RootPart::FindRootPartitions ();
            UI::CloseDialog ();
        }

        if (RootPart::numberOfValidRootPartitions == 0)
        {
            // a popup
            Popup::Message(_("No Linux root partition found."));
            return false;
        }

        // We must only change RootPart::selectedRootPartition if booting
        // is inevitable.
        string rp = fstab["root_partition"]:"";
        list<map> fstab_partitions = fstab["partitions"]:[];

        if (RootPart::numberOfValidRootPartitions == 1)
        {
            RootPart::SetSelectedToValid ();
        }
        else if ( rp == ""){
            Popup::Message(_("Multiple root partitions found, but you did not configure
which root partition should be used.  Automatic installation not possible.
"));
            return false;
        }
        else if ( haskey(RootPart::rootPartitions, rp) 
                && RootPart::numberOfValidRootPartitions > 1)
        {
            RootPart::selectedRootPartition = rp;
        }

        RootPart::MountPartitions(RootPart::selectedRootPartition);
        RootPart::SetFormatPartitions (fstab_partitions);
        RootPart::UnmountPartitions (true);
        return true;
    }


    /**
     * Create partition plan
     * @return boolean
     */
    global define boolean Write() {
        y2milestone("entering Write");
        Storage::SetRecursiveRemoval( true );

        if (read_fstab)
            return (handle_fstab());

        map <string, map> initial_target_map = Storage::GetTargetMap();
        y2milestone("Target map: %1", initial_target_map );

        Storage::SetPartDisk( find_next_disk(initial_target_map,"",`CT_DISK) );

        AutoTargetMap =  set_devices(AutoPartPlan);
        if (AutoTargetMap == nil || AutoTargetMap == $[])
            return false;

        y2milestone("AutoTargetMap: %1",  AutoTargetMap);
        
        // return list of available devices
        list<string> disk_devices = maplist( string k, map e,
                                             filter( string l, map f, Storage::GetTargetMap(),
                                                     ``(Storage::IsRealDisk( f ))), ``(k));

        y2milestone("disk_devices: %1", disk_devices );

        boolean result = false;

        foreach (string device, map data, AutoTargetMap, ``{
                if ( Storage::IsPartitionable(data) &&
                     data["initialize"]:false == true) {
                        initial_target_map[device,"delete"] = true;
                        if( haskey( data, "disklabel" ))
                            initial_target_map[device,"disklabel"] = data["disklabel"]:"msdos";
                        Storage::SetTargetMap(initial_target_map);
                }
        });
        y2milestone("Target map after initialzing disk: %1", Storage::GetTargetMap());

        foreach (string device, map data, AutoTargetMap, ``{
            string dlabel = data["label"]:"";
            if( size(filter( map e, data["partitions"]:[],
                  ``(e["mount"]:""==Partitions::BootMount() ||
                    (e["partition_id"]:0 == Partitions::FsidBoot(dlabel) && Partitions::FsidBoot(dlabel) != 131 ) ))) > 0 ) {
                planHasBoot = true;
                break;
            }
        });
        y2milestone("plan has boot: %1", planHasBoot);

        foreach (string device, map data, AutoTargetMap,
                 ``{
            if ( Storage::IsPartitionable(data) )
            {
                y2milestone("Creating partition plans for %1", device);
                
                map sol = find_matching_disk([device], Storage::GetTargetMap(), data );	  
                if (size(sol) > 0 )
                    result = true;
                    
                y2milestone("solutions: %1", sol);
                map <string, map> tm = Storage::GetTargetMap();
                tm[device] =    process_partition_data(device, sol);

        SearchRaids(tm);
                Storage::SetTargetMap(tm);
        }
        });

	if( haskey(AutoTargetMap, "/dev/nfs") )
	    {
	    y2milestone( "nfs:%1", AutoTargetMap["/dev/nfs"]:$[] );
	    foreach( map p, AutoTargetMap["/dev/nfs","partitions"]:[],
		    ``{
		    integer sizek = Storage::CheckNfsVolume( p["device"]:"", 
		                                             p["fstopt"]:"",
							     p["nfs4"]:false );
		    y2milestone( "nfs size:%1", sizek );
		    boolean ok = Storage::AddNfsVolume( p["device"]:"", p["fstopt"]:"",
		                                        sizek, p["mount"]:"",
							p["nfs4"]:false );
		    y2milestone( "nfs ok:%1", ok );
		    if( ok )
			Storage::ChangeVolumeProperties( p );
		    if( !result )
			result = ok && p["mount"]:""=="/";
		    });
	    y2milestone( "nfs result:%1", result );
	    }

        return result;
    }


    /**
     * Build the id for a partition entry in the man table.
     * @parm disk_dev_name name of the devie e.g.: /dev/hda
     * @parm nr number of the partition e.g.: 1
     * @return string e.g.: 01./dev/hda
     */
    define string  build_id(  string disk_dev_name , any nr )
    {
        return (sformat( "%1:%2", disk_dev_name, nr));
    }


    /**
     * Partitioning Overview
     * @return list Overview
     */
    global define list Overview()
    {
        y2milestone("entering Overview");

        map allfs = FileSystems::GetAllFileSystems(true, true);
        list drives_table = [];

        string id = "";
        foreach(map d, AutoPartPlan,
                ``{
            id = d["device"]:"";
            term a = `item(`id(id), d["device"]:"", "", "", "", "", "" );

            drives_table = add (drives_table, a);

            list<map> partitions = d["partitions"]:[];
            integer start_id = 1;

            if ( size (partitions) > 0 )
            {
                foreach(map p, partitions, ``{
                    id = build_id(d["device"]:"" , start_id );

                    term b = `item(`id(id));
                    b = add (b, "");

                    b=add(b, p["mount"]:"" );
                    b=add(b, p["size"]:"");

                    if (!haskey(p, "filesystem_id"))
                    {
                        b=add(b, Partitions::FsIdToString(p["partition_id"]:131));
                    }
                    else
                    {
                        b=add(b, Partitions::FsIdToString(p["filesystem_id"]:131));
                    }

                    map fs = allfs[p["filesystem"]:`nothing]:$[];
                    string fs_name = fs[`name]:"";

                    b=add(b, fs_name);

                    if ( size( p["region"]:[] ) > 0 )
                    {
                        b=add(b, sformat("%1 - %2", p["region",0]:0, p["region",1]:0));
                    }
                    else
                    {
                        b=add(b, "");
                    }

                    drives_table = add(drives_table, b);
                    start_id = start_id + 1;
                });
            }

        });

        integer entries = size(drives_table);
        list reversed_table = [];
        integer counter = entries;

        term tmp = `item(`id(`empty));

        while (counter != 0)
        {
            reversed_table=add(reversed_table, drives_table[counter -1]:tmp);
            counter = counter - 1;

        }
        y2debug("table: %1", drives_table );
        return drives_table;


    }

    // EOF
}
