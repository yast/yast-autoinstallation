/**
 * File:	clients/autoinst_scripts.ycp
 * Package:	Autoinstallation Configuration System
 * Summary:	Scripts
 * Authors:	Anas Nashif<nashif@suse.de>
 *
 * $Id$
 */
{
    textdomain "autoinst";

    import "AutoinstScripts";
    import "Wizard";
    import "Summary";

    import "Label";
    import "Popup";

    include "autoinstall/script_dialogs.ycp";

    any ret = nil;
    string func = "";
    map param = $[];

    /* Check arguments */
    if(size(WFM::Args()) > 0 && is(WFM::Args(0), string))
    {
	func = (string) WFM::Args(0);
	if(size(WFM::Args()) > 1 && is(WFM::Args(1), map))
	{
	    param = (map) WFM::Args(1);
	}
    }

    y2debug("func=%1", func);
    y2debug("param=%1", param);

    if (func == "Import")
    {
	ret = AutoinstScripts::Import(param);
    }
    /* create a  summary*/
    else if (func == "Summary")
    {
	AutoinstScripts::Import(param);
	ret =  AutoinstScripts::Summary();
    }
    else if (func == "Reset")
    {
	AutoinstScripts::Import($[]);
	ret =  $[];
    }
    else if (func == "GetModified")
    {
	ret = AutoinstScripts::GetModified();
    }
    else if (func == "Change") {
        Wizard::CreateDialog();
        Wizard::SetDesktopIcon("autoyast");
        any exp1 = AutoinstScripts::Export();
	ret = ScriptsDialog();
        any exp2 = AutoinstScripts::Export();
        if (exp1 != exp2)
            AutoinstScripts::SetModified();
	Wizard::CloseDialog();
    }
    else if (func == "Export")
    {
	ret = AutoinstScripts::Export();
    }
    /* unknown function */
    else {
	y2error("unknown function: %1", func);
	ret = false;
    }

    y2debug("ret=%1", ret);
    y2milestone("scripts auto finished");
    y2milestone("----------------------------------------");

    return ret;
}


