/**
 * File:        clients/ayast_probe.ycp
 * Package:     Auto-installation
 * Author:      Uwe Gansert <ug@suse.de>
 * Summary:     This client is more or less for debugging.
 *              It dumps interesting autoyast values and is
 *              not as scaring as reading y2log just for some info
 *
 * Changes:     * initial - just dumps the rule values
 * $Id$
 */

{
    import "Stage";
    Stage::Set( "initial" );
    import "AutoInstallRules";
    import "Label";
    import "UI";
    AutoInstallRules::ProbeRules();

    map<string,any> attrs = $[
                            "installed_product":AutoInstallRules::installed_product,
                            "installed_product_version":AutoInstallRules::installed_product_version,
                            "hostname":AutoInstallRules::hostname,
                            "hostaddress":AutoInstallRules::hostaddress,
                            "network":AutoInstallRules::network,
                            "domain":AutoInstallRules::domain,
                            "arch":AutoInstallRules::arch,
                            "karch":AutoInstallRules::karch,
                            "product":AutoInstallRules::product,
                            "product_vendor":AutoInstallRules::product_vendor,
                            "board_vendor":AutoInstallRules::board_vendor,
                            "board":AutoInstallRules::board,
                            "memsize":AutoInstallRules::memsize,
                            "totaldisk":AutoInstallRules::totaldisk,
                            "hostid":AutoInstallRules::hostid,
                            "mac":AutoInstallRules::mac,
                            "linux":AutoInstallRules::linux,
                            "others":AutoInstallRules::others,
                            "xserver":AutoInstallRules::xserver,
                            "haspcmcia":AutoInstallRules::haspcmcia,
/*
                            "NonLinuxPartitions":AutoInstallRules::NonLinuxPartitions,
                            "LinuxPartitions":AutoInstallRules::LinuxPartitions,
                            "disksize":AutoInstallRules::disksize
*/
                           ];
    string text="<h3>Keys for rules</h3><table>";
    foreach( string k, any v, attrs, ``{
        text = text + sformat("<tr><td>%1</td><td> = </td><td>%2<br></td></tr>",k,v);
    });
    text = text + "</table>";
    UI::OpenDialog( `opt(`defaultsize),
                `VBox(
                      `RichText( text ),
                      `PushButton(`opt(`default), Label::OKButton())
                      )
             );
  UI::UserInput();
  UI::CloseDialog();
}


