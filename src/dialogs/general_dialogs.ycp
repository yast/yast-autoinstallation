/**
 * File:	include/autoinstall/general_dialogs.ycp
 * Package:	Auto-installation
 * Summary:	General Settings
 * Author:	Anas Nashif <nashif@suse.de>
 *
 * $Id$
 */
{
    textdomain "autoinst";
    import "GetInstArgs";
    /**
     * Language
     * @return symbol
     */
    define symbol LanguageDialog() {
	return (symbol)
	WFM::CallFunction( "language_auto", [ GetInstArgs::Buttons(true,true) ] );
    }

    /**
     * Keyboard
     * @return symbol
     */
    define symbol KeyboardDialog() ``{
	return (symbol)
	WFM::CallFunction( "keyboard_auto", [ GetInstArgs::Buttons(true,true) ] );
    }


    /**
     * Timezone
     * @return symbol
     */
    define symbol TimezoneDialog() ``{
	return (symbol)
	WFM::CallFunction( "timezone_auto", [ GetInstArgs::Buttons(true,true) ]);
    }



    /**
     * Mouse
     * @return symbol
     */
    define symbol MouseDialog() ``{

	// create the wizard dialog
	//
	Wizard::CreateDialog();
	any result = WFM::CallFunction( "inst_mouse", [ GetInstArgs::Buttons(true,true) ] );
	return (symbol)result;
    }

    /**
     * Main dialog
     * @return symbol
     */
    define  symbol ModeDialog() ``{


	map mode		= AutoinstGeneral::mode;
	boolean forceboot	= mode["forceboot"]:false;
	boolean confirm	= mode["confirm"]:true;

	term contents =
	    `HVSquash(
		 `VBox(
		       `Left(`CheckBox(`id(`confirm), _("Con&firm installation?"), confirm)),
		       `Left(`CheckBox(`id(`forceboot), _("Start System without Reboot"), forceboot))

		       )
		 );

	string help_text = _("<P>
The options in this dialog control the behavior of the AutoYaST during
automatic installation.
</P>
");
	help_text = help_text + _("<P>
The installation confirmation option is selected by default
to avoid unwanted installation. It stops the system
during installation and shows a summary of requested operations in the
usual proposal screen.  Uncheck this option to install automatically without interruption.
</P>
");
	help_text = help_text + _("<P>
By default, the system reboots after the initial phase of
installation is completed. You can prevent the system from booting and continue
directly into the installed system. This is not recommended and should be used
only for debugging purposes.
</P>
");

	Wizard::SetContents(_("Other Options"),
			    contents, help_text, true , true);

	Wizard::HideAbortButton();
	Wizard::SetNextButton(`next, Label::FinishButton() );

	any ret = nil;
	repeat {
	    ret = UI::UserInput();
	    if (ret == `next)
	    {

		map m = $[];
		confirm = (boolean)UI::QueryWidget(`id(`confirm), `Value);
		forceboot = (boolean) UI::QueryWidget(`id(`forceboot), `Value);

		mode["forceboot"] = forceboot;
		mode["confirm"] = confirm;
		AutoinstGeneral::mode = mode;
	    }
	} until (ret == `next || ret == `back);
	return (symbol)ret;
    }



    /**
     * Dialog for General Settings
     * @return symbol
     */
    define symbol generalSequence ()
    {
	map dialogs = $[
			"language"	: ``(LanguageDialog()),
			"keyboard"	: ``(KeyboardDialog()),
			"timezone"	: ``(TimezoneDialog()),
			"mouse"		: ``(MouseDialog()),
			"mode"		: ``(ModeDialog())
	];

	map sequence = $[
			 "ws_start"	: "language",
			 "language"	: $[
					    `next	: "keyboard",
					    `abort	: `abort
			 ],
			 "keyboard"	: $[
					    `next	: "timezone",
					    `abort	: `abort
			 ],
			 "timezone"	: $[
					    `next	: "mouse",
					    `abort	: `abort
			 ],
			 "mouse"	: $[
					    `next	: "mode",
					    `abort	: `abort
			 ],
			 "mode"	:         $[
					    `next	: `finish
			                   ]
	                ];
	// Translators: dialog caption
	string caption = _("General Settings");
	term contents = `Label (_("Initializing ..."));

	Wizard::CreateDialog ();
	Wizard::SetContents ( caption,
			      contents,
			      "",
			      true,
			      true);


	any ret = Sequencer::Run(dialogs, sequence);

	Wizard::CloseDialog ();
	y2milestone(" generalSequence returns: %1", ret );
	return (symbol)ret;

    }



}
